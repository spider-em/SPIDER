([iter],[smallang-yn],[ampenhance],[ang-step],[ang-limit],[num-angs])

 ; <html><head><title>Sets reference projection angles</title></head><body><pre>
 ;
 ; PURPOSE: Creates list of reference projection angles in doc file
 ;
 ; SOURCE:  spider/docs/techs/recon1/Procs/refine_setrefangles.spi
 ;          From refine-inseries         ArDean Leith  May 2014
 ;
 ; INPUT REGISTERS:     
 ;       [iter]          Current iteration
 ;       [smallang-yn]   Flag to use small angle refinement (1 == Yes)
 ;
 ; OUTPUT REGISTERS:
 ;       [ampenhance]    Flag to use amplitude enhancement on this iteration
 ;       [ang-step]      Theta angular step              (may vary with iteration)
 ;       [ang-limit]     Restriction on angular search   (varies with iteration)
 ;       [num-angs]      Number of reference projection angles
 ;
 ; OUTPUT FILES:
 ;    [iter_refangs]     work/ang_refs_##         Ref angles doc file  (one/iter)
 ;    [ang_voea]         final/ang_voea           Ref angles doc file  (one)
 ;
 ; -- END BATCH HEADER -- ...................................................
 

 IF ( [smallang-yn] == 0 ) THEN  

  ; For normal angle refinement

  DE                     ; Remove existing doc file
   [iter_refangs]        ; Ref. angles doc file                   (removed)

  RR S [ang-step]        ; Get current angular step (varies with iteration)
     [ang-steps]         ; Angular steps for projection angle for all iterations (string)
     [iter]              ; Current iteration

   VO EA [num-angs]      ; Sets [num-angs] to number of reference projections
     [ang-step]          ; Theta angular step          (varies with iteration)
     0, 90               ; Theta range, 90 is for use with 'Check Mirrored Positions'
     0, 359.9            ; Phi range
     [iter_refangs]      ; Reference projection angles doc file   (output)

   RR S [ang-limit]      ; Get restriction on angular search   (varies with iteration)
     [ang-limits]        ; Restriction on angular search for all iterations (string)
     [iter]              ; Current iteration
   
 ELSE                    ; Small angle refinement

   ; For small angle polishing refinement around a determined position
   [ang-step] = [ang-step-sm]  ; Puts [ang-step] in register variable for return
   
   DE
     [ang_voea]          ; Reference projection angles doc file    (removed)

   VO EA [num-angs]      ; Sets [num-angs] to number of reference projections
     [ang-step-sm]       ; Theta angular step (from global register)               
     0, [theta-sm]       ; Theta range        (from global register) 
     0, 359.9            ; Phi range
     [ang_voea]          ; Reference projection angles doc file     (output)
 ENDIF
 

 RR S [ampenhance]       ; Amplitude enhancement flag (varies with iteration)
   [amp-enhance-flags]   ; Flags for each iteration (string)
   [iter]                ; Current iteration

 RE

 ; </pre></body></html>



