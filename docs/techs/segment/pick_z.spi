 ([dtype])

 ; MASTER COPY: spider/docs/techs/segment/seg_test.spi
 ;
 ; AUTHOR:  Tapu Shaikh    March 2024
 ;
 ; PURPOSE:  Picks z-slice if a volume
 ;           Copies if an image
 ;
 ; INPUT:    A SPIDER volume or image
 ;
 ; OUTPUT:   SPIDER image
 ;
 ; RETURNS:  Data type (1==image, 3==volume)

 FR
 ?Input image or volume?[invol]

 FR
 ?Output image?[outimg]

 RR [slicenum]
 ?z-slice?

 ; Check if volume or image
 FI H [dtype]  ; works with stacks even w/o trailing "@"
 [invol]
 IFORM

 IF ( [dtype] .EQ. 3 ) THEN
  ; Extract relevant slice from input volume
  PS Z                          ; Pick relevant slice
    [invol]                     ; Input volume
    [outimg]                    ; Output slice
    [slicenum]                  ; Slice number
 ELSEIF ( [dtype] .EQ. 1 ) THEN
  CP                            ; Copy
    [invol]                     ; Input volume
    [outimg]                    ; Output slice
 ELSE
   SYS M
     echo "ERROR!! Input has unknown data type: {%i0%[dtype]}" ;
     echo "  Exiting..."
.
     EN
 ENDIF

 RE
